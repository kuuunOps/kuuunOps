# 系统集成


## 构建工具集成
### 集成Maven

1. 安装maven

```shell
cat >/etc/profile.d/m2.sh <<EOF
export M2_HOME=/usr/local/apache-maven-3.8.1/
export PATH=\$PATH:\$M2_HOME/bin
EOF
source /etc/profile
mvn -version
```

2. 配置jenkins

![maven](../../_media/maven.png ':size=80%')

3. Jenkinsfile使用

```groovy
pipeline{
    agent { node { label 'master'}}
    stages{
        stage("Build"){
            steps{
                script{
                    MAVEN_HOME = tool "M2"
                    sh "${MAVEN_HOME}/bin/mvn -version"
                }
            }
        }
    }
}
```

maven常用命令

```shell
# 清理构建目录
mvn clean
# 打包
mvn package
# 打包部署
mvn clean install
# 单元测试
mvn test
```

4. 将构建命令使用选项参数传给pipeline使用（可选）

```groovy
#!/usr/bin/env groovy

String buildShell = "${env.buildShell}"

pipeline{
    agent { node { label 'master'}}
    stages{
        stage("Build"){
            steps{
                script{
                    MAVEN_HOME = tool "M2"
                    sh "${MAVEN_HOME}/bin/mvn ${buildShell}"
                }
            }
        }
    }
}
```

---

### 集成ant

1. 安装ant

```shell
cat >/etc/profile.d/ant.sh <<EOF
export ANT_HOME=/usr/local/apache-ant-1.10.10/
export PATH=\$PATH:\$ANT_HOME/bin
EOF
source /etc/profile
ant -version
```
2. 配置jenkins

![ant](../../_media/ant.png ':size=80%')

3. Jenkinsfile使用

```groovy
#!/usr/bin/env groovy

String buildShell = "${env.buildShell}"

pipeline{
    agent { node { label 'master'}}
    stages{
        stage("Maven Build"){
            steps{
                script{
                    MAVEN_HOME = tool "M2"
                    sh "${MAVEN_HOME}/bin/mvn ${buildShell}"
                }
            }
        }
        stage("Ant Build"){
            steps{
                script{
                    ANT_HOME = tool "ANT"
                    sh "${ANT_HOME}/bin/ant ${buildShell}"
                }
            }
        }
    }
}
```

---

### 集成Gradle

1. 安装gradle

```shell
cat >/etc/profile.d/gradle.sh <<EOF
export GRADLE_HOME=/usr/local/gradle-7.0/
export PATH=\$PATH:\$GRADLE_HOME/bin
EOF
source /etc/profile
gradle -version
```
2. 配置jenkins

![gradle](../../_media/gradle.png ':size=80%')

3. Jenkinsfile使用

```groovy
#!/usr/bin/env groovy

String buildShell = "${env.buildShell}"

pipeline{
    agent { node { label 'master'}}
    stages{
        stage("Maven Build"){
            steps{
                script{
                    MAVEN_HOME = tool "M2"
                    try{
                        sh "${MAVEN_HOME}/bin/mvn ${buildShell}"
                    }catch(e){
                        println(e)
                    }
                }
            }
        }
        stage("Ant Build"){
            steps{
                script{
                    ANT_HOME = tool "ANT"
                    try{
                        sh "${ANT_HOME}/bin/ant ${buildShell}"
                    }catch(e){
                        println(e)
                    }
                }
            }
        }
        stage("Gradle Build"){
            steps{
                script{
                    GRADLE_HOME = tool "GRADLE"
                    sh "${GRADLE_HOME}/bin/gradle ${buildShell}"
                }
            }
        }
    }
}

```
---
### 集成npm

>1. 安装node

```shell
cat >/etc/profile.d/node.sh <<EOF
export NODE_HOME=/usr/local/node-v14.16.1-linux-x64
export PATH=\$PATH:\$NODE_HOME/bin
EOF
source /etc/profile
node -v
npm -version
```
>2. 配置jenkins

![node](../../_media/nodejs.png ':size=80%')

>3. Jenkinsfile使用

```groovy
#!/usr/bin/env groovy

String buildShell = "${env.buildShell}"

pipeline{
    agent { node { label 'master'}}
    stages{
        stage("Maven Build"){
            steps{
                script{
                    MAVEN_HOME = tool "M2"
                    //增加异常处理
                    try{
                        sh "${MAVEN_HOME}/bin/mvn ${buildShell}"
                    }catch(e){
                        println(e)
                    }
                }
            }
        }
        stage("Ant Build"){
            steps{
                script{
                    ANT_HOME = tool "ANT"
                    try{
                        sh "${ANT_HOME}/bin/ant ${buildShell}"
                    }catch(e){
                        println(e)
                    }
                }
            }
        }
        stage("Gradle Build"){
            steps{
                script{
                    GRADLE_HOME = tool "GRADLE"
                    sh "${GRADLE_HOME}/bin/gradle ${buildShell}"
                }
            }
        }
        stage("Npm Build"){
            steps{
                script{
                    NODE_HOME = tool "NODE"
                    try{
                        sh """
                            ${NODE_HOME}/bin/node -v
                            ${NODE_HOME}/bin/npm ${buildShell}
                        """
                    }catch(hudson.AbortException e){
                        println(e)
                        sh """
                            PATH=$PATH:${NODE_HOME}/bin
                            ${NODE_HOME}/bin/node -v
                            ${NODE_HOME}/bin/npm ${buildShell}
                        """
                    }
                }
            }
        }
    }
}
```

---

### 共享库封装构建工具

>1. 创建库文件

```groovy
package org.devops

//构建
def build(btype,bshell){
    def buildTools = ["mvn":"M2","ant":"ANT","gradle":"GRADLE","npm":"NODE"]

    buildHome = tool buildTools[btype]
    if ("${btype}" == "npm"){
        sh """
            PATH=$PATH:${buildHome}/bin
            ${buildHome}/bin/${btype} ${bshell}
        """
    } else {
        sh "${buildHome}/bin/${btype} ${bshell}"
    }
}
```

>2. 配置jenkins

![buildtype](../../_media/buildtype.png ':size=80%')

![buildshell](../../_media/buildshell.png ':size=80%')

>3. 修改Jenkinsfile

```groovy
#!/usr/bin/env groovy
@Library("jenkinslibrary") _

def build = new org.devops.Build()

String buildType = "${env.buildType}"
String buildShell = "${env.buildShell}"

pipeline{
    agent { node { label 'master'}}
    stages{
        stage("Maven Build"){
            steps{
                script{
                    build.build(buildType,buildShell)
                }
            }
        }
    }
}
```
---
## 部署工具集成
### 集成saltstack

>1. 安装saltstack

- 配置yum

```shell
sudo rpm --import https://repo.saltproject.io/py3/redhat/7/x86_64/latest/SALTSTACK-GPG-KEY.pub
curl -fsSL https://repo.saltproject.io/py3/redhat/7/x86_64/latest.repo | sudo tee /etc/yum.repos.d/salt.repo
```
- 安装

```
sudo yum install salt-master
sudo yum install salt-minion
sudo yum install salt-ssh
sudo yum install salt-syndic
sudo yum install salt-cloud
sudo yum install salt-api
```

>2. 编写jenkinslibrary

```groovy
/*
src/org/devops/Deploy.groovy
*/
package org.devops

def saltDeploy(hosts,command){
    sh "salt -L ${hosts} ${command}"
}
```

>3. Jenkinsfile使用

```groovy
#!/usr/bin/env groovy
@Library("jenkinslibrary") _

def build = new org.devops.Build()
def deploy = new org.devops.Deploy()

String buildType = "${env.buildType}"
String buildShell = "${env.buildShell}"
// 新增字符串参数
String deployHosts = "${env.deployHosts}"

pipeline{
    agent { node { label 'master'}}
    stages{
        stage("Build"){
            steps{
                script{
                    build.build(buildType,buildShell)
                }
            }
        }
        stage("Salt Deploy"){
            steps{
                script{
                    deploy.saltDeploy("${deployHosts}","test.ping")
                }
            }
        }
    }
}
```
---

### 集成ansible

>1. 安装ansible

```shell
yum install epel-release -y
yum install ansible -y
```

>2. 编写jenkinslibrary

```groovy
/*
src/org/devops/Deploy.groovy
*/
package org.devops

def saltDeploy(hosts,command){
    sh "salt -L ${hosts} ${command}"
}

def ansibleDeploy(hosts,command,options=""){
    if("${options}"== ""){
        sh "ansible ${hosts} -m ${command}"
    }else{
        sh "ansible ${hosts} -m ${command} -a ${options}"
    }
}
```

>3. 编写Jenkinsfile

```groovy
#!/usr/bin/env groovy
@Library("jenkinslibrary") _

def build = new org.devops.Build()
def deploy = new org.devops.Deploy()

String buildType = "${env.buildType}"
String buildShell = "${env.buildShell}"
String deployHosts = "${env.deployHosts}"


pipeline{
    agent { node { label 'master'}}
    stages{
        stage("Build"){
            steps{
                script{
                    build.build(buildType,buildShell)
                }
            }
        }
        stage("Salt Deploy"){
            when {
                environment name: 'deployTools', value: 'salt'
            }
            steps{
                script{
                    deploy.saltDeploy("${deployHosts}","test.ping")
                }
            }
        }
        stage("Ansible Deploy"){
            when{
                environment name: 'deployTools',value: "ansible"
            }
            steps{
                script{
                    deploy.ansibleDeploy("${deployHosts}","ping")
                }
            }
        }
    }
}
```
---

## 认证系统集成
### LDAP/AD集成

>1. 修改安全配置

![secret](../../_media/anybody.png ':size=80%')

>2. 安装`LDAP`/`Active Directory`

>3. 配置jenkins

>4. 测试


---

### Gitlab集成

>1. 安装插件`Gitlab Authentication`

>2. 调整全局安全策略

![secret](../../_media/anybody.png ':size=80%')

>3. 配置Gitlab，生成应用访问信息

>jenkins回调地址：http://JENKINS_HOST:8080/securityRealm/finishLogin

![gitlab](../../_media/gitlab-applications.png  ':size=80%')

![gitlab](../../_media/gitlab-applications1.png  ':size=80%')

>4. 配置jenkins

![jenkins](../../_media/jenkins-gitlab.png ':size=80%')

>5. 测试

---

### Github集成

>1. 安装插件`GitHub Authentication`

>2. 调整全局安全策略

![secret](../../_media/anybody.png ':size=80%')

>3. 配置Github，生成应用访问信息

![github](../../_media/github-app.png ':size=80%')

![github](../../_media/github-jenkins.png ':size=80%')

![github](../../_media/github-auth.png ':size=80%')

>4. 配置jenkins

![github](../../_media/jenkins-github.png  ':size=80%')

>5. 测试

---

## 版本控制系统集成-Gitlab

>上传演示代码：https://github.com/kuuun-devops/simple-java-maven-app

>Jenkins构建流水线

编写Jenkinsfile

```groovy
#!/usr/bin/env groovy
@Library("jenkinslibrary") _

def build = new org.devops.Build()
def deploy = new org.devops.Deploy()

String buildType = "${env.buildType}"
String buildShell = "${env.buildShell}"
String deployHosts = "${env.deployHosts}"
String srcUrl = "${env.srcUrl}"
String branchName = "${env.branchName}"

pipeline{
    agent { node { label 'master'}}
    stages{
        stage("CheckOut"){
            steps{
                script{
                    checkout([$class: 'GitSCM', branches: [[name: '${branchName}']], extensions: [], userRemoteConfigs: [[credentialsId: 'a3dc9163-faf6-4362-a0ae-7217b907fc3e', url: '${srcUrl}']]])
                }
            }
        }
        stage("Maven Build"){
            when {
                environment name: "buildType",value: "mvn"
            }
            steps{
                script{
                    build.build(buildType,buildShell)
                }
            }
        }

    }
}
```
### 配置Gitlab提交流水线

>插件：`Generic Webhook Trigger`

>1. 配置触发器

![jenkins](../../_media/jenkins-webhook-1.png ':size=80%')

![jenkins](../../_media/jenkins-webhook-2.png ':size=80%')

>2. 配置Gitlab，添加hook

![gitlab](../../_media/gitlab-webhook.png ':size=80%')


---
### 配置分支字段匹配

>1. 配置jenkins

![jenkins](../../_media/jenkins-json-ref.png ':size=80%')

>2. 修改Jenkinsfile

```groovy
if("${runOpts}"=="GitlabPush"){
    // 获取分支名称
    branchName = branch - "refs/heads/"
}
```

>3. 测试

---
### 增加构建描述信息

>变量：`currentBuild.description`

>1. 配置Jenkins

![jenkins](../../_media/jenkins-json-username.png ':size=80%')

>2. 编写Jenkinsfile

```groovy
if("${runOpts}"=="GitlabPush"){
    branchName = branch - "refs/heads/"
    currentBuild.description = "Trigger by ${userName} ${branch}!"
}
```

>3. 测试

---

### 增加Gitlab提交构建状态

>Gitlab的pipeline接口
>- 状态：`pending`,`running`,`success`,`failed`,`canceled`
>- API: `POST /projects/:id/statuses/:sha`
>- 官方参考文档：https://docs.gitlab.com/ce/api/commits.html#post-the-build-status-to-a-commit


>1. Gitlab创建个人访问令牌
>2. 编写jenkinslibrary,构建请求

```groovy
package org.devops

def gitlabHttpAPI(httpHost,crtId,action,url){
    withCredentials([string(credentialsId: crtId, variable: 'ACCESS_TOKEN')]) {
        res = httpRequest contentType: 'APPLICATION_JSON', 
                customHeaders: [[name: 'PRIVATE-TOKEN', value: "${ACCESS_TOKEN}"]], 
                httpMode: action, 
                ignoreSslErrors: true, 
                responseHandle: 'NONE', 
                url: "${httpHost}/api/v4/${url}", 
                wrapAsMultipart: false
    }
    return res
}

def gitlabBuildStatus(httpUrl,crtId,projectId,commitSha,status){
    String httpHost = getHttpHost(httpUrl)
    String url = "projects/${projectId}/statuses/${commitSha}?state=${status}"
    String response = gitlabHttpAPI(httpHost,crtId,'POST',url)
    println(response)
    return response
}

def getHttpHost(httpUrl){
    String protocol = httpUrl.split(':')[0]
    String host = httpUrl.split('/')[2]
    return "${protocol}://${host}"
}
```

>3. 配置流水线

>新增参数解析

- `httpUrl`
- `projectId`
- `commitSha`
>4. 引入共享库

```groovy
def gitlab = new org.devops.Gitlab()
String crtId = "190b00a4-2c2d-4141-ab67-93310cbdf856"

if("${runOpts}"=="GitlabPush"){
    branchName = branch - "refs/heads/"
    currentBuild.description = "Trigger by ${userName} ${branch}!"
    gitlab.gitlabBuildStatus(httpUrl,crtId,projectId,commitSha,"running")
}
```

>5. 测试

---
### 过滤push请求

>参考文献：`https://github.com/jenkinsci/generic-webhook-trigger-plugin/tree/master/src/test/resources/org/jenkinsci/plugins/gwt/bdd`

**配置jenkins**

>增加变量

- `object_kind`:`$.object_kind`
- `before`:`$.before`
- `after`:`$.after`

>增加过滤

![jenkins](../../_media/jenkins-filter.png ':size=80%')

---

### 构建邮件通知

>插件：`Email Extension Template`

>1. jenkins配置邮件

>2. 编写邮件发送模块

```groovy
/*
src/org/devops/Email.groovy
*/
package org.devops

def sendEmail(status,toUser){
    emailext attachLog: true, 
        compressLog: true,
        body: """
            <!DOCTYPE html> 
            <html> 
            <head> 
            <meta charset="UTF-8"> 
            </head> 
            <body leftmargin="8" marginwidth="0" topmargin="8" marginheight="4" offset="0"> 
                <img src="${JENKINS_URL}/static/aaaf8f1b/images/jenkins-header-logo-v2.svg">
                <table width="95%" cellpadding="0" cellspacing="0" style="font-size: 11pt; font-family: Tahoma, Arial, Helvetica, sans-serif">   
                    <tr> 
                        <td><br /> 
                            <b><font color="#0B610B">构建信息</font></b> 
                        </td> 
                    </tr> 
                    <tr> 
                        <td> 
                            <ul> 
                                <li>项目名称：${JOB_NAME}</li>         
                                <li>构建编号：${BUILD_ID}</li> 
                                <li>构建状态: ${status} </li>                         
                                <li>项目地址：<a href="${BUILD_URL}">${BUILD_URL}</a></li>    
                                <li>构建日志：<a href="${BUILD_URL}console">${BUILD_URL}console</a></li> 
                            </ul> 
                        </td> 
                    </tr> 
                    <tr>  
                </table> 
            </body> 
            </html>  """,
            subject: "Jenkins-${JOB_NAME}项目构建信息 ",
            to: toUser
}
```

>3. Jenkinsfile引入

```groovy
def email = new org.devops.Email()
String userEmail = "${env.userEmail_0}"

post{
    success {
        script{
            println("success")
            gitlab.gitlabBuildStatus(httpUrl,crtId,projectId,commitSha,"success")
            email.sendEmail("流水线成功",userEmail)
        }
    }
}

```

>4. 测试

---
### 合并流水线

---
## 代码质量系统SonarQube集成

### 安装SonarQube

### 安装Sonar-Scan-Cli

>执行命令示例

```shell
sonar-scanner  -Dsonar.host.url=http://172.16.4.72:9000  \
-Dsonar.projectKey=demo-maven-service  \
-Dsonar.projectName=demo-maven-service  \
-Dsonar.projectVersion=1.0 \
-Dsonar.login=admin \
-Dsonar.password=admin \
-Dsonar.ws.timeout=30 \
-Dsonar.projectDescription="my first project!"  \
-Dsonar.links.homepage=http://www.baidu.com \
-Dsonar.sources=src \
-Dsonar.sourceEncoding=UTF-8 \
-Dsonar.java.binaries=target/classes \
-Dsonar.java.test.binaries=target/test-classes \
-Dsonar.java.surefire.report=target/surefire-reports
```

### 原生Groovy，编写jenksinsharlibrary

>

```groovy
package org.devops
/*
src/org/devops/Sonarqube.groovy
*/
def sonarJava(sonarServe,username,password,projectName,projectDescription,projectPath){
    String sonarDate = sh returnStdout:true,script:"date +%Y%m%d%H%M%S"
    sonarDate = sonarDate - '\n'
    
    sh """
    /usr/local/sonar-scanner-cli/bin/sonar-scanner  -Dsonar.host.url=${sonarServe}  \
        -Dsonar.login=${username} \
        -Dsonar.password=${password} \
        -Dsonar.ws.timeout=30 \
        -Dsonar.projectKey=${projectName}  \
        -Dsonar.projectName=${projectName} \
        -Dsonar.projectVersion=${sonarDate} \
        -Dsonar.projectDescription=${projectDescription}  \
        -Dsonar.links.homepage=http://www.baidu.com \
        -Dsonar.sources=${projectPath} \
        -Dsonar.sourceEncoding=UTF-8 \
        -Dsonar.java.binaries=target/classes \
        -Dsonar.java.test.binaries=target/test-classes \
        -Dsonar.java.surefire.report=target/surefire-reports
    """
}
```


### 原生Groovy，Jeknisfile引用

```groovy
stage("QA"){
    steps{
        script{
            tools.printMsg("代码扫描","green")
            sonarqueb.sonarJava("http://172.16.4.72:9000","admin","admin",JOB_NAME,JOB_NAME,"src")
        }
    }
}
```
---
### 基于jenkins插件，编写jenksinsharlibrary

>- 文档参考：`https://docs.sonarqube.org/latest/analysis/scan/sonarscanner-for-jenkins`
>- 安装插件：`SonarQube Scanner for Jenkins`
>- 配置SonarServe

```groovy
package org.devops
/*
src/org/devops/Sonarqube.groovy
*/
def sonarJava(sonarServer,projectName,projectDescription,projectPath){

    def servers = ["test":"sonarqube-test","dev":"sonarqube-dev","pro":"sonarqube-pro"]

    withSonarQubeEnv(servers[sonarServer]) {
        // This expands the evironment variables SONAR_CONFIG_NAME, SONAR_HOST_URL, SONAR_AUTH_TOKEN that can be used by any script.
        // https://docs.sonarqube.org/latest/analysis/scan/sonarscanner-for-jenkins
        
        String SONAR_SCAN_CLI_HOME = "/usr/local/sonar-scanner-cli"
        String sonarDate = sh returnStdout:true,script:"date +%Y%m%d%H%M%S"
        sonarDate = sonarDate - '\n'
        
        sh """
            ${SONAR_SCAN_CLI_HOME}/bin/sonar-scanner \
            -Dsonar.ws.timeout=30 \
            -Dsonar.projectKey=${projectName}  \
            -Dsonar.projectName=${projectName} \
            -Dsonar.projectVersion=${sonarDate} \
            -Dsonar.projectDescription=${projectDescription}  \
            -Dsonar.links.homepage=http://www.baidu.com \
            -Dsonar.sources=${projectPath} \
            -Dsonar.sourceEncoding=UTF-8 \
            -Dsonar.java.binaries=target/classes \
            -Dsonar.java.test.binaries=target/test-classes \
            -Dsonar.java.surefire.report=target/surefire-reports
        """
    }
}

```
### 基于jenkins插件，Jeknisfile引用

```groovy
stage("QA"){
    steps{
        script{
            tools.printMsg("代码扫描","green")
            sonarqueb.sonarJava("test",JOB_NAME,JOB_NAME,"src")
        }
    }
}
```

---

### 质量配置


---
### 质量阈

>检查状态回调jenkins
>
>回调地址：`http://JENKINS_HOST/sonarqube-webhook/`

1. 在sonarqube生成webhook认证信息

>配置-->网络调用-->创建

![sonarqube](../../_media/jenkins-sonarqube-webhook.png ':size=80%')

2. 在jenkins配置webhook认证信息

>系统管理-->系统配置-->`SonarQube servers`

![sonarqube](../../_media/jenkins-sonarqube-webhook-2.png ':size=80%')

3. 修改jenkinslibrary

```groovy
package org.devops

def sonarJava(sonarServer,projectName,projectDescription,projectPath){

    def servers = ["test":"sonarqube-test","dev":"sonarqube-dev","pro":"sonarqube-pro"]

    withSonarQubeEnv(servers[sonarServer]) {
        // This expands the evironment variables SONAR_CONFIG_NAME, SONAR_HOST_URL, SONAR_AUTH_TOKEN that can be used by any script.
        // https://docs.sonarqube.org/latest/analysis/scan/sonarscanner-for-jenkins
        
        String SONAR_SCAN_CLI_HOME = "/usr/local/sonar-scanner-cli"
        String sonarDate = sh returnStdout:true,script:"date +%Y%m%d%H%M%S"
        sonarDate = sonarDate - '\n'
        
        sh """
            ${SONAR_SCAN_CLI_HOME}/bin/sonar-scanner \
            -Dsonar.ws.timeout=30 \
            -Dsonar.projectKey=${projectName}  \
            -Dsonar.projectName=${projectName} \
            -Dsonar.projectVersion=${sonarDate} \
            -Dsonar.projectDescription=${projectDescription}  \
            -Dsonar.links.homepage=http://www.baidu.com \
            -Dsonar.sources=${projectPath} \
            -Dsonar.sourceEncoding=UTF-8 \
            -Dsonar.java.binaries=target/classes \
            -Dsonar.java.test.binaries=target/test-classes \
            -Dsonar.java.surefire.report=target/surefire-reports
        """
    }

    // 增加以下字段
    timeout(time: 1, unit: 'HOURS') {
        def qg = waitForQualityGate()
        if (qg.status != 'OK') {
        error "Pipeline aborted due to quality gate failure: ${qg.status}"
        }
    }
}
```

4. 基于API地址获取状态（可选）

>API接口地址：`http://172.16.4.72:9000/api/project_branches/list?project=demo-maven-service_PUSH`


>jenkinslibrary

```groovy
// 请求HTTP的URL进行拼接
def getSonarStatus(httpHost,projectName,crtId){
    String url  = "project_branches/list?project=${projectName}"
    String apiUrl = "${httpHost}/api/${url}"

    response = sonarHttpRequest(apiUrl,crtId)
    responseJSON = readJSON text: """${response.content}"""
    
    return responseJSON.branches[0].status.qualityGateStatus
}

// 发起GET请求
def sonarHttpRequest(apiUrl,crtId){
    res = httpRequest authentication: crtId, 
        contentType: 'APPLICATION_JSON', 
        ignoreSslErrors: true, 
        responseHandle: 'NONE', 
        url: apiUrl, 
        wrapAsMultipart: false

    return res
}
```

>Jenkinsfile

```groovy
sonarStatus = sonarqube.getSonarStatus("http://172.16.4.72:9000",JOB_NAME,sonarJenkinsCrtId)
if(sonarStatus.toString() == "ERROR"){
    email.sendEmail("代码扫描错误，请修复",userEmail)
    error "代码扫描错误，请修复"
}
```

---
### 基于API查询项目

```groovy
// 查找项目
def searchProject(apiHost,projectName,crtId){
    String apiUrl  = "${apiHost}/api/projects/search?projects=${projectName}"

    response = sonarHttpRequest(apiUrl,crtId,"GET")
    responseJSON = readJSON text: """${response.content}"""
    
    result = responseJSON.paging.total.toString()
    if(result == "0"){
        return false
    }
    return responseJSON
}
```

---
### 基于API创建项目

```groovy
// 创建项目
def createProject(apiHost,projectName,projectKey,crtId){
    String apiUrl  = "${apiHost}/api/projects/create?name=${projectName}&projects=${projectKey}"

    response = sonarHttpRequest(apiUrl,crtId,"POST")
    responseJSON = readJSON text: """${response.content}"""

    return responseJSON
}
```

### 质量配置

```groovy
//  质量配置
def configQualityProfile(apiHost,crtId,language,projectKey,qualityProfile){
    String apiUrl  = "${apiHost}/api/qualityprofiles/add_project?language=${language}&project=${projectKey}&qualityProfile=${qualityProfile}"

    response = sonarHttpRequest(apiUrl,crtId,"POST")
    responseJSON = readJSON text: """${response.content}"""
    println(responseJSON)
    return responseJSON
}
```

---
### 质量阈

```groovy
//  获取质量阈ID
def getGateId(apiHost,crtId,gateName){
    String apiUrl  = "${apiHost}/api/qualitygates/show?name=${gateName}"
    response = sonarHttpRequest(apiUrl,crtId,"POST")
    responseJSON = readJSON text: """${response.content}"""
    println(responseJSON)
    id = responseJSON.get("id")

    if ( id == null ){
        error "gateName is not exist!"
        return false
    }

    return id.toString()
}

//  关联质量阈
def configQualityGates(apiHost,crtId,gateName,projectKey){

    def gateId=getGateId(apiHost,crtId,gateName)
    String apiUrl  = "${apiHost}/api/qualitygates/select?gateId=${gateId}&projectKey=${projectKey}"

    response = sonarHttpRequest(apiUrl,crtId,"POST")
    responseJSON = readJSON text: """${response.content}"""
    println(responseJSON)
    return responseJSON
}
```

---
## 制品库Nexus集成

### Neuxs的安装及配置

### Nexus制品上传-mvn命令

```shell
mvn deploy:deploy-file -DgroupId=<group-id> \
  -DartifactId=<artifact-id> \
  -Dversion=<version> \
  -Dpackaging=<type-of-packaging> \
  -Dfile=<path-to-file> \
  -DrepositoryId=<id-to-map-on-server-section-of-settings.xml> \
  -Durl=<url-of-the-repository-to-deploy>
```
示例

```shell
# 手动上传命令
mvn deploy:deploy-file -Dmaven.test.skip=true \
    -DgroupId=com.mycompany.app \
    -DartifactId=my-app \
    -Dversion=1.0-SNAPSHOT \
    -Dpackaging=jar \
    -Dfile=./target/my-app-1.0-SNAPSHOT.jar \
    -DrepositoryId=demo-hosted \
    -Durl=http://REPO_HOST/repository/demo-hosted
```
>集成jenkins上传

```groovy
def jarName = sh returnStdout: true, script: "cd target;ls *.jar"
jarName = jarName - "\n"
println(jarName)

String serverId = "demo-hosted"
String repoUrl = "http://REPO_HOST/repository/demo-hosted"

def pom = readMavenPom file: 'pom.xml'
pomGroupId = pom.groupId
pomArtifactId = pom.artifactId
pomVersion = pom.version
pomPackaging = pom.packaging

println("debug: ${pomGroupId} ${pomArtifactId} ${pomVersion} ${pomPackaging}")

def m2Home = tool "M2"

sh """
    ${m2Home}/bin/mvn deploy:deploy-file -Dmaven.test.skip=true \
    -DgroupId=${pomGroupId} \
    -DartifactId=${pomArtifactId} \
    -Dversion=${pomVersion} \
    -Dpackaging=${pomPackaging} \
    -Dfile=./target/${jarName} \
    -DrepositoryId=${serverId}\
    -Durl=${repoUrl}
"""
```
---

### Nexus制品上传-jenkins插件

>插件：`Nexus Artifact Uploader`

```groovy
tools.printMsg("上传制品","green")
def jarName = sh returnStdout: true, script: "cd target;ls *.jar"
jarName = jarName - "\n"
println(jarName)

String serverId = "demo-hosted"
String repoHost = "maven.ibumobile.com"

def pom = readMavenPom file: 'pom.xml'
pomGroupId = pom.groupId
pomArtifactId = pom.artifactId
pomVersion = pom.version
pomPackaging = pom.packaging

println("debug: ${pomGroupId} ${pomArtifactId} ${pomVersion} ${pomPackaging}")

// jenkins插件上传
String fileName = "target/${jarName}"
nexusArtifactUploader artifacts:[[artifactId:"${pomArtifactId}",classifier:"",file:"${fileName}",type:"${pomPackaging}"]], 
    credentialsId: "46d45cc8-b463-41bd-b52b-f98875c98a6e",
    groupId: "${pomGroupId}",
    nexusUrl: "${repoHost}",
    nexusVersion: "nexus3", 
    protocol: "https", 
    repository: "${serverId}", 
    version: "${pomVersion}"
```

---

### 选择制品下载发布

>插件：`Maven Artifact ChoiceListProvider (Nexus)`

>配置jenkins构建参数化构建

![nexus](../../_media/maven-nexus.png ':size=80%')

---

### 制品晋级流水线

>插件：`Maven Artifact ChoiceListProvider (Nexus)`

```groovy
def artifactUpdate(repoPotocol,repoHost,repoName,certId,repoSnapshotUrl,artifactUrl){
    sh "wget ${artifactUrl}"
    
    // 获取pomInfo
    // [, com, mycompany, app, my-app, 1.0-SNAPSHOT, my-app-1.0-20210430.082831-6.jar]
    pomInfo = artifactUrl.minus(repoSnapshotUrl).split("/").toList()

    // 获取pomGroupId
    env.pomGroupId = pomInfo[0..2].join(".")

    // 获取pomArtifactId
    env.pomArtifactId = pomInfo[3]

    // 获取pomVersion
    env.pomVersion = pomInfo[4].replace("SNAPSHOT","RELEASE")

    // 获取jarName
    // my-app-1.0-20210430.082831-6.jar
    jarName = pomInfo[-1]

    // 获取pomPackaging
    // jar
    env.pomPackaging = jarName.split("\\.")[-1]

    // 设置新的jarName
    env.fileName = "${pomArtifactId}-${pomVersion}.${pomPackaging}"

    // jar重命名
    sh "mv ${jarName} ${fileName}"

    // 上传制品
    pluginUpload(repoPotocol,repoHost,repoName,certId)
}
```

---

### Nexus Rest API

---

### 集成Jfrog artifactory

>插件：