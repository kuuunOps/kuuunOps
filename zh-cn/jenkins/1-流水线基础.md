# 流水线基础

>一条流水线通过Jenkinsfile进行描述

声明式插件：`Pipeline: Declarative`

Jenkinsfile文件结构

- 指定节点agent
- 指定运行的选项options
- 指定运行阶段stages
- 指定构建后操作post

## Pipeline定义-agent/options

```groovy
agent {
    node {
        label "master" // 指定运行节点的标签或者名称
        customWorkspace "${workspace}" //指定运行工作目录
    }
}
options {
    timestamps() //日志会有时间
    skipDefaultCheckout() //删除隐式checkout scm语句
    disableConcurrentBuilds() //禁止并行
    timeout(time: 1, unit: 'HOURS') //流水线超时设置1h
}
```

## Pipeline定义-stages

>指定阶段

```groovy
stages {
    stage("GetCode"){
        steps {
            timeout(time:5, unit:"MINUTES"){
                script{
                    println('获取代码')
                }
            }
        }
    }
    stage("Build"){
        steps {
            timeout(time:20, unit:"MINUTES"){
                script{
                    println('应用打包')
                }
            }
        }
    }
    stage("CodeScan"){
        steps {
            timeout(time:30, unit:"MINUTES"){
                script{
                    println('代码扫描')
                }
            }
        }
    }
}
```
## Pipeline定义-post

>构建后的操作

- `always`：总是执行的脚本片段
- `success`：成功后执行
- `failure`：失败后执行
- `aborted`：取消后执行

```groovy
post {
    always {
        script{
            println("always")
        }
    }
    success {
        script{
            currentBuild.description += "\n 构建成功"
        }
    }
    failure {
        script{
            currentBuild.description += "\n 构建失败"
        }
    }
    aborted {
        script{
            currentBuild.description += "\n 构建取消"
        }
    }
}
```

## 完整示例代码

```groovy
#!groovy

String workspace = "/opt/jenkins/workspace"

pipeline{
    //指定运行此流水线的节点
    agent { 
        node { 
            label "master"
            customWorkspace "${workspace}"
        }
        
    }
    
    options {
        timestamps() //日志会有时间
        skipDefaultCheckout() //删除隐式checkout scm语句
        disableConcurrentBuilds() //禁止并行
        timeout(time: 1, unit: 'HOURS') //流水线超时设置1h
    }
    stages {
        stage("GetCode"){
            steps {
                timeout(time:5, unit:"MINUTES"){
                    script{
                        println('获取代码')
                    }
                }
            }
        }
        stage("Build"){
            steps {
                timeout(time:20, unit:"MINUTES"){
                    script{
                        println('应用打包')
                    }
                }
            }
        }
        stage("CodeScan"){
            steps {
                timeout(time:30, unit:"MINUTES"){
                    script{
                        println('代码扫描')
                    }
                }
            }
        }
    }
    post {
        always {
            script{
                println("always")
            }
        }
        success {
            script{
                currentBuild.description += "\n 构建成功"
            }
        }
        failure {
            script{
                currentBuild.description += "\n 构建失败"
            }
        }
        aborted {
            script{
                currentBuild.description += "\n 构建取消"
            }
        }
    }
}
```
---

## Pipeline语法-agent

>agent指定流水线的执行节点

参数：

- `any`：在任何可用的节点上执行`pipeline`
- `none`：没有指定agent的时候默认
- `lable`：指定标签上的节点上运行`pipeline`
- `node`：运行额外选项

```groovy
agent { node { label 'label name'}}
// 等效于 agent { label 'label name' }
```

## Pipeline语法-post

>定义一个或多个steps，这些阶段根据流水线或阶段的完成情况而运行（取决于流水线中post部分的位置）。post支持以下post-condition块中的其中之一。

| 状态       | 描述                                       |
| ---------- | ------------------------------------------ |
| `always`   | 无论流水线或者阶段的完成状态               |
| `changed`  | 只有当流水线或者阶段完成状态与之前不同时。 |
| `failure`  | 只有当流水线或者阶段状态为`failure`运行。  |
| `success`  | 只有当流水线或者阶段状态为`success`运行。  |
| `unstable` | 只有当流水线或者阶段状态为`unstable`运行   |
| `aborted`  | 只有当流水线或者阶段状态为`aborted`运行    |

## Pipeline语法-stages

>包含一系列一个或者多个stage指令，建议stages至少包含一个stage指令用于连续交付过程的每个离散部分。

```groovy
pipeline {
    agent any
    stages {
        stage('Example'){
            steps {
                echo "Hello World"
            }
        }
    }
}
```

## Pipeline语法-steps

>step是每个阶段中要执行的每个步骤

```groovy
pipeline {
    agent any
    stages {
        stage('Example'){
            steps {
                echo "Hello World"
            }
        }
    }
}
```

## Pipeline语法-environment

>environment指令指定一个键值对序列，该序列将被定义为所有步骤的环境变量，或者是特定于阶段的步骤，这取决于environment指令在流水线内的位置。
>
>该指令支持一个特殊的方法credentials()，该方法可以用于Jenkins环境中通过标识符访问预定义的凭证。
>
>- 类型为`Secret Text`的凭证：credentials()将确保指定的环境变量包含密文的文本内容。
>- 类型为`Standart username and password`的凭证：指定的环境变量指定为username:password，并且两个额外的环境变量将被自动定义为：`MYVARNAME_USR`和`MYVARNAME_PSW`。

```groovy
pipeline {
    agent any
    environment {
        CC = 'clang'
    }
    stages{
        stage('Example'){
            environment {
                AN_ACCESS_KEY = credentials('my-prefined-secret-text')
            }
            steps {
                sh 'printenv'
            }
        }
    }
}
```

## Pipeline语法-options

options指令允许从流水线内部配置特定于流水线的选项。

| 参数                      | 描述                                                                                |
| ------------------------- | ----------------------------------------------------------------------------------- |
| `buildDiscarder`          | 为最近的流水线允许的特定数量保存组件和控制台输出                                    |
| `disableConcurrentBuilds` | 不允许同时执行流水线                                                                |
| `overrideIndexTriggers`   | 允许覆盖分支索引触发器的默认处理                                                    |
| `skipDefaultCheckout`     | 在agent指令中，跳过从源代码控制中检出代码的默认情况                                 |
| `skipStagesAfterUnstable` | 一旦构建状态变得`UNSTABLE`，跳过该阶段                                              |
| `checkotToSubdirectory`   | 在工作空间的子目录中自动地执行源代码控制检出                                        |
| `timeout`                 | 设置流水线的超时时间，在此之后，Jenkins将中止流水线                                 |
| `retry`                   | 在失败时候，整改流水线重新尝试的次数                                                |
| `timestamps`              | 预测所有流水线生成的控制台输出，与该流水线发出的时间一致。需要插件支持`Timestamper` |


## Pipeline语法-paramters

>在流水线运行时设置项目相关的参数

- string字符串类型的参数

```
paramters { string{name: 'DEPLOY_ENV', defaultValue: 'staging', description: ''}}
```

- booleanParam布尔参数

```
paramters { booleanParam{name: 'DEBUG_BUILD', defaultValue: true, description: ''}}
```

## Pipeline语法-trigger

>触发器

- cron 计划任务定期执行构建

```
triggers { cron('H */4 * * 1-5')}
```

- pollSCM与cron定义类型，但是由Jenkins定期检测源码变化

```
triggers { pollSCM('H */4 * * 1-5')}
```

- upstream 接受逗号分隔的工作字符串和阈值。当字符串中的任何作业以最小阈值结束时，流水线被重新触发。

```
triggers { upstream(upstreamProjects: 'job1,job2',threshold: hudson.model.Result.SUCCESS)}
```

## Pipeline语法-tool

>获取通过自动安装或手动放置的工具的环境变量。

```groovy
pipeline {
    agent ant
    tools {
        maven 'apace-maven-3.6.1'
    }
    stages {
        stage('Example'){
            steps{
                sh 'mvn -version'
            }
        }
    }
}
```

## Pipeline语法-input

>input用户在执行各个阶段的时候，有人工确认是否继续进行。

```groovy
pipeline {
    agent any
    stages {
        stage('Example') {
            input {
                message "Should we continue?"
                ok "Yes, we should."
                submitter "alice,bob"
                parameters {
                    string(name: 'PERSON', defaultValue: 'Mr Jenkins', description: 'Who should I say hello to?')
                }
            }
            steps {
                echo "Hello, ${PERSON}, nice to meet you."
            }
        }
    }
}
```

| 配置项             | 描述                                                                      |
| ------------------ | ------------------------------------------------------------------------- |
| message            | 必需的。 这将在用户提交 input 时呈现给用户。                              |
| id                 | input 的可选标识符， 默认为 stage 名称。                                  |
| ok                 | `input`表单上的"ok" 按钮的可选文本。                                      |
| submitter          | 可选的以逗号分隔的用户列表或允许提交 input 的外部组名。默认允许任何用户。 |
| submitterParameter | 环境变量的可选名称。如果存在，用 submitter 名称设置。                     |
| parameters         | 提示提交者提供的一个可选的参数列表。                                      |

## Pipeline语法-when

>when 指令允许流水线根据给定的条件决定是否应该执行阶段。 when 指令必须包含至少一个条件。 如果 when 指令包含多个条件, 所有的子条件必须返回True，阶段才能执行。

**内置条件**

- branch

>当正在构建的分支与模式给定的分支匹配时，执行这个阶段

```
when { branch 'master' }
```

- environment

>当指定的环境变量是给定的值时，执行这个步骤,

```
when { environment name: 'DEPLOY_TO', value: 'production' }
```

- expression

>当指定的Groovy表达式评估为true时，执行这个阶段

```
when { expression { return params.DEBUG_BUILD } }
```

- not

>当嵌套条件是错误时，执行这个阶段,必须包含一个条件

```
when { not { branch 'master' } }
```

- allOf

>当所有的嵌套条件都正确时，执行这个阶段,必须包含至少一个条件

```
when { allOf { branch 'master'; environment name: 'DEPLOY_TO', value: 'production' } }
```

- anyOf

>当至少有一个嵌套条件为真时，执行这个阶段,必须包含至少一个条件，

```
when { anyOf { branch 'master'; branch 'staging' } }
```

## Pipeline语法-parallel

>声明式流水线的阶段可以在他们内部声明多隔嵌套阶段, 它们将并行执行。 注意，一个阶段必须只有一个 steps 或 parallel 的阶段。 嵌套阶段本身不能包含进一步的 parallel 阶段, 但是其他的阶段的行为与任何其他 stage 相同。任何包含 parallel 的阶段不能包含 agent 或 tools 阶段, 因为他们没有相关 steps。
>
>另外, 通过添加 failFast true 到包含 parallel的 stage 中， 当其中一个进程失败时，你可以强制所有的 parallel 阶段都被终止。

```groovy
pipeline {
    agent any
    stages {
        stage('Non-Parallel Stage') {
            steps {
                echo 'This stage will be executed first.'
            }
        }
        stage('Parallel Stage') {
            when {
                branch 'master'
            }
            failFast true
            parallel {
                stage('Branch A') {
                    agent {
                        label "for-branch-a"
                    }
                    steps {
                        echo "On Branch A"
                    }
                }
                stage('Branch B') {
                    agent {
                        label "for-branch-b"
                    }
                    steps {
                        echo "On Branch B"
                    }
                }
            }
        }
    }
}
```

## Pipeline语法-steps

>script 步骤需要 scripted-pipeline块并在声明式流水线中执行。 对于大多数用例来说,应该声明式流水线中的“脚本”步骤是不必要的， 但是它可以提供一个有用的"逃生出口"。非平凡的规模和/或复杂性的 script 块应该被转移到 共享库 

```groovy
pipeline {
    agent any
    stages {
        stage('Example') {
            steps {
                echo 'Hello World'

                script {
                    def browsers = ['chrome', 'firefox']
                    for (int i = 0; i < browsers.size(); ++i) {
                        echo "Testing the ${browsers[i]} browser"
                    }
                }
            }
        }
    }
}
```

## JenkinsShareLibrary-概述

## 定义共享库

>共享库通过名称，源代码检索方法，比如 SCM，以及可选的默认版本定义。名称应该是一个简短的标识符，因为它将在脚本中使用。

>该 SCM 可以理解该版本的任何内容；比如，分支、标签和提交哈希值，这些都适用于 Git。你还可以声明脚本是否需要显式地请求该库，或者在默认情况下是否存在该库。此外，如果你在 Jenkins 配置中指定了版本，则可以阻止脚本选择不同的版本。

>指定 SCM 最好的方式是使用一个专门更新的 SCM 插件，以支持新的 API 检出任意命名的版本 (Modern SCM 选项)。 在撰写本文时，最新版本的 Git 和 Subversion 插件支持这种模式；

## 目录结构

- src:目录应该看起来像标准的 Java 源目录结构。当执行流水线时，该目录被添加到类路径下。
- vars:目录定义可从流水线访问的全局变量的脚本。
- resources:目录允许从外部库中使用 libraryResource 步骤来加载有关的非 Groovy 文件。

```
(root)
+- src                     # Groovy source files
|   +- org
|       +- foo
|           +- Bar.groovy  # for org.foo.Bar class
+- vars
|   +- foo.groovy          # for global 'foo' variable
|   +- foo.txt             # help for 'foo' variable
+- resources               # resource files (external libraries only)
|   +- org
|       +- foo
|           +- bar.json    # static helper data for org.foo.Bar
```

## JenkinsShareLibrary-使用

>标记为 Load implicitly 的共享库允许流水线立即使用任何此库定义的类或全局变量。为了访问其他共享库， Jenkinsfile 需要使用 @Library 注解， 指定库的名字：

![share](../../_media/pipelinelibrary.png ':size=80%')

```groovy
@Library('my-shared-library') _
/* Using a version specifier, such as branch, tag, etc */
@Library('my-shared-library@1.0') _
/* Accessing multiple libraries with one statement */
@Library(['my-shared-library', 'otherlib@abc1234']) _
```

## JenkinsShareLibrary-实践

- 实践内容：格式化输出
- 项目地址：https://gitee.com/kuuun/jenkinslibrary.git
- 依赖插件：`AnsiColor`

```groovy
package org.devops

def printMsg(msg,color){
    colors = [
        'red': "\033[31m >>>>>>>>>>>>>>>>>>>> ${msg} <<<<<<<<<<<<<<<<<<<< \033[0m",
        'blue': "\033[34m >>>>>>>>>>>>>>>>>>>> ${msg} <<<<<<<<<<<<<<<<<<<< \033[0m",
        'green': "\033[32m >>>>>>>>>>>>>>>>>>>> ${msg} <<<<<<<<<<<<<<<<<<<< \033[0m"
    ]
    ansiColor('xterm'){
        println(colors[color])
    }
}
```

Jenkinsfile引用

```groovy
@Library("jenkinslibrary") _

def tools = new org.devops.Tools()

pipeline{
    agent any
    stages{
        stage("Example"){
            steps{
                scripts{
                    tools.printMsg("This is test","red")
                }
            }
        }
    }
}
```

## 代码生成器

![代码生层器](../../_media/snippet.png ':size=80%')


## 系统全局变量

## env

>可以从Groovy代码中以`env.VARNAME`或仅以`VARNAM`E的形式访问环境变量。

使用示例

```groovy
env.MYTOOL_VERSION = '1.33'
node {
  sh '/usr/local/mytool-$MYTOOL_VERSION/bin/start'
}
```

```
BRANCH_NAME
For a multibranch project, this will be set to the name of the branch being built, for example in case you wish to deploy to production from master but not from feature branches; if corresponding to some kind of change request, the name is generally arbitrary (refer to CHANGE_ID and CHANGE_TARGET).
CHANGE_ID
For a multibranch project corresponding to some kind of change request, this will be set to the change ID, such as a pull request number, if supported; else unset.
CHANGE_URL
For a multibranch project corresponding to some kind of change request, this will be set to the change URL, if supported; else unset.
CHANGE_TITLE
For a multibranch project corresponding to some kind of change request, this will be set to the title of the change, if supported; else unset.
CHANGE_AUTHOR
For a multibranch project corresponding to some kind of change request, this will be set to the username of the author of the proposed change, if supported; else unset.
CHANGE_AUTHOR_DISPLAY_NAME
For a multibranch project corresponding to some kind of change request, this will be set to the human name of the author, if supported; else unset.
CHANGE_AUTHOR_EMAIL
For a multibranch project corresponding to some kind of change request, this will be set to the email address of the author, if supported; else unset.
CHANGE_TARGET
For a multibranch project corresponding to some kind of change request, this will be set to the target or base branch to which the change could be merged, if supported; else unset.
CHANGE_BRANCH
For a multibranch project corresponding to some kind of change request, this will be set to the name of the actual head on the source control system which may or may not be different from BRANCH_NAME. For example in GitHub or Bitbucket this would have the name of the origin branch whereas BRANCH_NAME would be something like PR-24.
CHANGE_FORK
For a multibranch project corresponding to some kind of change request, this will be set to the name of the forked repo if the change originates from one; else unset.
TAG_NAME
For a multibranch project corresponding to some kind of tag, this will be set to the name of the tag being built, if supported; else unset.
TAG_TIMESTAMP
For a multibranch project corresponding to some kind of tag, this will be set to a timestamp of the tag in milliseconds since Unix epoch, if supported; else unset.
TAG_UNIXTIME
For a multibranch project corresponding to some kind of tag, this will be set to a timestamp of the tag in seconds since Unix epoch, if supported; else unset.
TAG_DATE
For a multibranch project corresponding to some kind of tag, this will be set to a timestamp in the format as defined by java.util.Date#toString() (e.g., Wed Jan 1 00:00:00 UTC 2020), if supported; else unset.
BUILD_NUMBER
The current build number, such as "153"
BUILD_ID
The current build ID, identical to BUILD_NUMBER for builds created in 1.597+, but a YYYY-MM-DD_hh-mm-ss timestamp for older builds
BUILD_DISPLAY_NAME
The display name of the current build, which is something like "#153" by default.
JOB_NAME
Name of the project of this build, such as "foo" or "foo/bar".
JOB_BASE_NAME
Short Name of the project of this build stripping off folder paths, such as "foo" for "bar/foo".
BUILD_TAG
String of "jenkins-${JOB_NAME}-${BUILD_NUMBER}". All forward slashes ("/") in the JOB_NAME are replaced with dashes ("-"). Convenient to put into a resource file, a jar file, etc for easier identification.
EXECUTOR_NUMBER
The unique number that identifies the current executor (among executors of the same machine) that’s carrying out this build. This is the number you see in the "build executor status", except that the number starts from 0, not 1.
NODE_NAME
Name of the agent if the build is on an agent, or "master" if run on master
NODE_LABELS
Whitespace-separated list of labels that the node is assigned.
WORKSPACE
The absolute path of the directory assigned to the build as a workspace.
WORKSPACE_TMP
A temporary directory near the workspace that will not be browsable and will not interfere with SCM checkouts. May not initially exist, so be sure to create the directory as needed (e.g., mkdir -p on Linux). Not defined when the regular workspace is a drive root.
JENKINS_HOME
The absolute path of the directory assigned on the master node for Jenkins to store data.
JENKINS_URL
Full URL of Jenkins, like http://server:port/jenkins/ (note: only available if Jenkins URL set in system configuration)
BUILD_URL
Full URL of this build, like http://server:port/jenkins/job/foo/15/ (Jenkins URL must be set)
JOB_URL
Full URL of this job, like http://server:port/jenkins/job/foo/ (Jenkins URL must be set)
SCM-specific variables such as GIT_COMMIT are not automatically defined as environment variables; rather you can use the return value of the checkout step.

As an example of loading variable values from Groovy:

mail to: 'devops@acme.com',
    subject: "Job '${JOB_NAME}' (${BUILD_NUMBER}) is waiting for input",
    body: "Please go to ${BUILD_URL} and verify the build"
```

---

## params

>将构建中定义的所有参数暴露为具有各种类型值的只读映射

```groovy
if (params.BOOLEAN_PARAM_NAME) {doSomething()}
```

---
## currentBuild

>指当前构建的任务运行中，可读的属性。

---

## 常用的DSL

>参考文档：https://www.jenkins.io/zh/doc/pipeline/steps/

## readJSON-Json数据格式化

>参考地址：https://www.jenkins.io/doc/pipeline/steps/pipeline-utility-steps/

>插件：`Pipeline Utility Steps`

```groovy
def props = readJSON text: '{ "key": "value" }'
assert props['key'] == 'value'
assert props.key == 'value'
```
## withCredentials

```groovy
withCredentials([
    usernamePassword(
        credentialsId: '794b4e3b-e53b-4985-911a-52e51cf3c639', 
        passwordVariable: 'PASSWORD', 
        usernameVariable: 'USERNAME'
        )
]) {
    // some block
}
```

## checkout

- **Git**

```groovy
checkout([
    $class: 'GitSCM', 
    branches: [[name: '*/master']], 
    extensions: [], 
    userRemoteConfigs: [[
        credentialsId: 'c1395130-1f2e-4a5e-a5c5-b23ec2a9c7a6', 
        url: 'https://gitee.com/kuuun/jenkinslibrary.git'
        ]]
])
```

- **SVN**

```groovy
checkout([
    $class: 'SubversionSCM', 
    additionalCredentials: [], 
    excludedCommitMessages: '', 
    excludedRegions: '', 
    excludedRevprop: '', 
    excludedUsers: '', 
    filterChangelog: false, 
    ignoreDirPropChanges: false, 
    includedRegions: '', 
    locations: [
        [
            cancelProcessOnExternalsFail: true, 
            credentialsId: 'eee429de-a14b-4ea4-82c1-4e5fbaee23a8', 
            depthOption: 'infinity', 
            ignoreExternalsOption: true, 
            local: '.', 
            remote: 'http://svn.local/devops'
            ]
    ], 
    quietOperation: true, 
    workspaceUpdater: [$class: 'UpdateUpdater']
])
```

## publishHTML

>插件：`HTML Publisher

```groovy
publishHTML([
    allowMissing: false, 
    alwaysLinkToLastBuild: false, 
    keepAll: false, 
    reportDir: 'reports', 
    reportFiles: 'index.html', 
    reportName: 'HTML Report', 
    reportTitles: ''
    ])
```

## input

```groovy
input message: '是否继续？', 
ok: '是的', 
parameters: [
    choice(choices: ['Alice', 'John'], description: '', name: 'NAME')
    ], 
submitter: 'admin'
```

## BuildUser

>插件：`build user vars`

```groovy
wrap([$class: 'BuildUser']) {
    def user = env.BUILD_USER_ID
}
```

| Variable              | Description                        |
| --------------------- | ---------------------------------- |
| BUILD_USER            | Full name (first name + last name) |
| BUILD_USER_FIRST_NAME | First name                         |
| BUILD_USER_LAST_NAME  | Last name                          |
| BUILD_USER_ID         | Jenkins user ID                    |
| BUILD_USER_GROUPS     | Jenkins user groups                |
| BUILD_USER_EMAIL      | Email address                      |

## rtUpload

>插件：`Artifactory`

```groovy
rtUpload(
    serverID: "artifactory01",
    spec:
        """{
            "files": [
                "pattern": "report/a.html",
                "target": "${repoName}/a/b/v"
            ]
        }
        """
)
```

## httpRequest

>插件：`HTTP Request`

```groovy
apiUrl = "http://xxx.com/api/project_branches/list?project=${projectName}"
result = httpRequest authentication: "xxxx",
    quiet: true,
    contentType: 'APPLICATION_JSON',
    url: "${apiUrl}"
```

## email

>插件：`Email Extension`

```groovy
emailext body: '"邮件测试邮件内容"', subject: '邮件测试', to: 'admin@example.com'
```

---

## Groovy数据类型-String

## 字符串表示

```groovy
name = "groovy"
name = 'groovy'
name = """
    groovy
    jenkins
"""
```

## 常见方法

- `contains()`

>是否包含特定内容

```groovy
groovy> tools="devops-jenkins-groovy" 
groovy> println(tools.contains("devops")) 
 
true
```

- `size()/length()`

>返回字符串的长度

```groovy
groovy> tools="devops-jenkins-groovy"  
groovy> println(tools.length())  
 
21

```

- `indexOf()`

>返回元素索引

```groovy
groovy> tools="devops-jenkins-groovy"  
groovy> println(tools.indexOf("jenkins")) 
 
7
```

- `endsWith()`

>是否以什么特定字符结尾

```groovy
groovy> tools="devops-jenkins-groovy"  
groovy> println(tools.endsWith("groovy")) 
 
true
```

- `minus()/plus()`

>去掉/增加字符串，返回新的字符串

```groovy
groovy> tools="devops-jenkins-groovy"  
groovy> println(tools.minus("-jenkins")) 
groovy> println(tools.plus("-python")) 
 
devops-groovy
devops-jenkins-groovy-python
```
等同于

```groovy
groovy> tools="devops-jenkins-groovy"   
groovy> println(tools - "-jenkins")  
groovy> println(tools + "-python")  
 
devops-groovy
devops-jenkins-groovy-python

```

- `reverse()`

>反序

```groovy
groovy> tools="devops-jenkins-groovy"  
groovy> println(tools.reverse()) 
 
yvoorg-sniknej-spoved
```

- `substring(1,2)`

>返回指定索引范围的字符串

```groovy
groovy> tools="devops-jenkins-groovy"  
groovy> println(tools.substring(7,14)) 
 
jenkins
```

- `toUpperCase()/toLowerCase()`

>字符串大小写转换

```groovy
groovy> tools="Devops-Jenkins-Groovy" 
groovy> println(tools.toUpperCase())   
groovy> println(tools.toLowerCase()) 
 
DEVOPS-JENKINS-GROOVY
devops-jenkins-groovy

```

- `split()`

>按指定标识分隔字符串，默认为空格，返回列表数据类型

```groovy
groovy> tools="Devops-Jenkins-Groovy" 
groovy> println(tools.split('-')) 
 
[Devops, Jenkins, Groovy]
```

---

## Groovy数据类型-List

>列表表示：`[]`

## 常用方法

- `add()`

>增加元素

```groovy
groovy> tools=["Devops","Jenkins","Groovy"] 
groovy> tools.add("Python") 
groovy> println(tools) 
 
[Devops, Jenkins, Groovy, Python]
```
等同于

```groovy
groovy> tools=["Devops","Jenkins","Groovy"] 
groovy> tools += "Python" 
groovy> println(tools) 
 
[Devops, Jenkins, Groovy, Python]

```
列表合并

```groovy
groovy> tools=["Devops","Jenkins","Groovy"] 
groovy> tools2=["Gitlab","Python"] 
groovy> print(tools + tools2) 
 
[Devops, Jenkins, Groovy, Gitlab, Python]
```

- `remove()`

>删除元素

```groovy
groovy> tools=["Devops","Jenkins","Groovy"] 
groovy> tools.remove("Jenkins") 
groovy> println(tools) 
 
[Devops, Groovy]
```
等同于

```groovy
groovy> tools=["Devops","Jenkins","Groovy"] 
groovy> tools -= "Jenkins" 
groovy> println(tools) 
 
[Devops, Groovy]

```

- `isEmpty()`

>判断是否为空列表

```groovy
groovy> tools=["Devops","Jenkins","Groovy"]  
groovy> println(tools.isEmpty()) 
 
false

```

- `join()`

>按指定的标识进行拼接，返回一个字符串

```groovy
groovy> tools=["Devops","Jenkins","Groovy"]  
groovy> println(tools.join("-")) 
 
Devops-Jenkins-Groovy

```

- `contains()`

>是否包含某个元素

```groovy
groovy> tools=["Devops","Jenkins","Groovy"]  
groovy> println(tools.contains("Jenkins")) 
 
true

```

- `reverse()`

>反序

```groovy
groovy> tools=["Devops","Jenkins","Groovy"] 
groovy> println(tools.reverse()) 
 
[Groovy, Jenkins, Devops]

```

- `sort()`

>升序

```groovy
groovy> tools=["Devops","Jenkins","Groovy"] 
groovy> println(tools.sort()) 
 
[Devops, Groovy, Jenkins]

```

- `size()`

>返回元素个数

```groovy
groovy> tools=["Devops","Jenkins","Groovy"] 
groovy> println(tools.size()) 
 
3
```

- `unique()`

>去重

```groovy
groovy> tools=["Devops","Jenkins","Groovy","Jenkins"] 
groovy> println(tools.unique()) 
 
[Devops, Jenkins, Groovy]

```

- `each{}`

>遍历

```groovy
groovy> tools=["Devops","Jenkins","Groovy"] 
groovy> tools.each{ 
groovy>     println(it) 
groovy> } 
 
Devops
Jenkins
Groovy
```

---

## Groovy数据类型-Map

>表示方法：[:]

## 常用方法

- `get()`

>获取元素

```groovy
groovy> person=["name":"John","age":"18","job":"Devops"] 
groovy> println(person.get("name")) 
 
John

groovy> person=["name":"John","age":"18","job":"Devops"] 
groovy> println(person["name"]) 
 
John
groovy> person=["name":"John","age":"18","job":"Devops"] 
groovy> println(person.name) 
 
John

```

- `containsKey()`

>是否包含某个key

```groovy
groovy> person=["name":"John","age":"18","job":"Devops"] 
groovy> println(person.containsKey("name")) 
 
true

```

- `keySet()`

>以列表的形式返回key的集合

```groovy
groovy> person=["name":"John","age":"18","job":"Devops"] 
groovy> println(person.keySet()) 
 
[name, age, job]

```

- `remove()`

>删除某个key的元素的，并返回这个元素的value，同时修改原map

```groovy
groovy> person=["name":"John","age":"18","job":"Devops"] 
groovy> println(person.remove("job")) 
groovy> println(person) 
 
Devops
[name:John, age:18]

```

- `each{}`

>遍历

```groovy
groovy> person=["name":"John","age":"18","job":"Devops"] 
groovy> person.each{ 
groovy>     println(it) 
groovy> } 
 
name=John
age=18
job=Devops

```

- `values()`

>以列表的形式返回value的值

```groovy
groovy> person=["name":"John","age":"18","job":"Devops"] 
groovy> println(person.values()) 
 
[John, 18, Devops]

```
---

## Groovy条件语句-if

```groovy
groovy> num = 10 
groovy> if (num >5 ){ 
groovy>     println("这个数字比5大") 
groovy> }else{ 
groovy>     println("这个数字比5小") 
groovy> } 
 
这个数字比5大

```
---
## Groovy条件语句-switch

```groovy
groovy> tool = "jenkins" 
groovy> switch(tool){ 
groovy>     case "jenkins": 
groovy>         println("Jenkins") 
groovy>         break; 
groovy>     case "gitlab": 
groovy>         println("Gitlab") 
groovy>         break; 
groovy>     default: 
groovy>         println("Other") 
groovy> } 
 
Jenkins
```
---
## Groovy循环语句-for

```groovy
groovy> tools=["Devops","Jenkins","Groovy"] 
groovy> for (i in tools){ 
groovy>     println(i) 
groovy> } 
 
Devops
Jenkins
Groovy
groovy> person=["name":"John","age":"18","job":"Devops"]  
groovy> for(i in person){ 
groovy>     println(i) 
groovy> } 
 
name=John
age=18
job=Devops

person=["name":"John","age":"18","job":"Devops"] 

groovy> person=["name":"John","age":"18","job":"Devops"]  
groovy> for(key in person.keySet()){ 
groovy>     println(person["${key}"]) 
groovy> } 
 
John
18
Devops

```

## Groovy循环语句-while

```groovy
groovy> int sum = 0 
groovy> int i = 1 
groovy> while(i <=100){ 
groovy>     sum += i 
groovy>     i++ 
groovy> } 
groovy> println(sum) 
 
5050
```

---

## Groovy函数

```groovy
groovy> def printHello(String name){ 
groovy>     println("Hi ${name}!") 
groovy> } 
groovy> printHello("John") 
 
Hi John!
```
返回值

```groovy
groovy> def printHello(String name){  
groovy>     println("Hi ${name}!")  
groovy>     return name 
groovy> }  
groovy> res = printHello("John")  
groovy> println(res) 
 
Hi John!
John

```

---

## Groovy正则表达式

```groovy
groovy> name = "/ref/head/master" 
groovy> match = (name =~ /.*\/head\/(.*)/) 
groovy> println(match.find()) 
groovy> println(match[0]) 
groovy> println(match[0][1]) 
 
true
[/ref/head/master, master]
master

```
---